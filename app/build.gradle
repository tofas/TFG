apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.google.gms.google-services'

def supportVersion = "23.0.1"

def versionMajor = 1
def versionMinor = 0
def versionPatch = 1

def keystorePropertiesFile = file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "com.tfg.dani.tfg"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        alpha {
        }

        beta {
        }

        production {

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(path: ':backend', configuration: 'android-endpoints')
    compile 'com.google.android.gms:play-services-auth:8.3.0'
    //MVP: http://hannesdorfmann.com/mosby/
    compile 'com.hannesdorfmann.mosby:mvp:2.0.0'
    compile 'com.hannesdorfmann.mosby:viewstate:2.0.0'
    //Http: Retrofit
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    //UI: https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:7.0.1'
    compile "com.android.support:appcompat-v7:$supportVersion"
    //Reactive java
    compile 'io.reactivex:rxandroid:1.0.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.0.16'

    compile 'com.android.support:design:23.0.0'
}

play {
    jsonFile = file('google-play-publish-keys.json')
    serviceAccountEmail = "tfg-65@api-5010132945513051672-806562.iam.gserviceaccount.com"
    track = "${track}"
}
